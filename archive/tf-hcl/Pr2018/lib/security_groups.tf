resource "aws_security_group" "alb" {
  name_prefix = "${var.environment_tag}-alb-${random_id.deployment.hex}-"
  vpc_id      = aws_vpc.main.id

  ingress {
    description = "HTTPS"
    from_port   = 443
    to_port     = 443
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    description = "HTTP redirect to HTTPS"
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name        = "${var.environment_tag}-alb-sg-${random_id.deployment.hex}"
    Environment = var.environment_tag
  }
}

resource "aws_security_group" "app" {
  name_prefix = "${var.environment_tag}-app-${random_id.deployment.hex}-"
  vpc_id      = aws_vpc.main.id

  ingress {
    description     = "HTTP from ALB"
    from_port       = 80
    to_port         = 80
    protocol        = "tcp"
    security_groups = [aws_security_group.alb.id]
  }
  ingress {
    description = "SSH from VPC only"
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = [var.vpc_cidr]
  }
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
  tags = {
    Name        = "${var.environment_tag}-app-sg-${random_id.deployment.hex}"
    Environment = var.environment_tag
  }
}

resource "aws_security_group" "db" {
  name_prefix = "${var.environment_tag}-db-${random_id.deployment.hex}-"
  vpc_id      = aws_vpc.main.id

  ingress {
    description     = "MySQL/Aurora"
    from_port       = 3306
    to_port         = 3306
    protocol        = "tcp"
    security_groups = [aws_security_group.app.id]
  }
  tags = {
    Name        = "${var.environment_tag}-db-sg-${random_id.deployment.hex}"
    Environment = var.environment_tag
  }
}