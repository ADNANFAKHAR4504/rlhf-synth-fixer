import groovy.xml.XmlParser

plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
}

//compileJava {
//    dependsOn("NormalizeCDKFImports")
//}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

repositories {
    maven {
        url("https://maven-central.storage-download.googleapis.com/maven2/")
    }
    mavenCentral()
    mavenLocal()
}

// Version management
ext {
    pulumiVersion = '1.16.1'
    awsCdkVersion = '2.204.0'
    hashicorpCdktfVersion = '0.21.0'
    guavaVersion = '33.0.0-jre'
    jacksonVersion = '2.17.1'
    awsSdkVersion = '2.25.16'
    junitVersion = '5.10.2'
    mockitoVersion = '5.11.0'
    assertjVersion = '3.24.2'
}

// Resolve dependency conflicts
configurations.configureEach {
    resolutionStrategy {
        force "com.google.guava:guava:${guavaVersion}"
        eachDependency { details ->
            if (details.requested.group == 'com.google.collections' && details.requested.name == 'google-collections') {
                details.useTarget "com.google.guava:guava:${guavaVersion}"
                details.because 'google-collections is replaced by guava'
            }
        }
    }
}

dependencies {
    // Pulumi dependencies
    implementation "com.pulumi:pulumi:${pulumiVersion}"
    implementation 'com.pulumi:aws:7.5.0'
    implementation "com.hashicorp:cdktf:${hashicorpCdktfVersion}"
    implementation 'com.pulumi:tls:5.2.0'

    // AWS CDK dependencies
    implementation "software.amazon.awscdk:aws-cdk-lib:${awsCdkVersion}"
    implementation "software.constructs:constructs:10.4.2"

    // Utilities
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "com.google.guava:guava:${guavaVersion}"

    // Test dependencies
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
}

application {
    mainClass = 'app.Main'
}

// Override run task to handle environment properly
tasks.named('run') {
    standardInput = System.in
    workingDir = projectDir
    systemProperties = System.getProperties()
    environment = System.getenv()
}

// Configure source directories
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'lib/src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['tests/unit/java']
        }
    }
    integrationTest {
        java {
            srcDirs = ['tests/integration/java']
        }
        resources {
            srcDirs = ['tests/integration/resources']
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

// Configure integration test configurations
configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

// Integration test dependencies
dependencies {
    // AWS SDK BOM for consistent versions
    integrationTestImplementation platform("software.amazon.awssdk:bom:${awsSdkVersion}")
    integrationTestImplementation 'software.amazon.awssdk:s3'
    integrationTestImplementation 'software.amazon.awssdk:ec2'
    integrationTestImplementation 'software.amazon.awssdk:iam'
    integrationTestImplementation 'software.amazon.awssdk:kms'
    integrationTestImplementation 'software.amazon.awssdk:cloudformation'
    integrationTestImplementation 'software.amazon.awssdk:cloudtrail'
    integrationTestImplementation 'software.amazon.awssdk:cloudwatch'
    integrationTestImplementation 'software.amazon.awssdk:cloudwatchlogs'
    integrationTestImplementation 'software.amazon.awssdk:sns'
    integrationTestImplementation 'software.amazon.awssdk:sts'
    integrationTestImplementation 'software.amazon.awssdk:elasticloadbalancingv2'
    integrationTestImplementation 'software.amazon.awssdk:autoscaling'

    // JSON parsing for integration tests
    integrationTestImplementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    integrationTestImplementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
}

// Configure JAR with main class
jar {
    manifest {
        attributes(
                'Main-Class': 'app.Main'
        )
    }
    archiveFileName = 'app.jar'
}

// Create a fat JAR task separately if needed
tasks.register('fatJar', Jar) {
    group = 'build'
    description = 'Creates a fat JAR with all dependencies'
    archiveClassifier = 'fat'

    manifest {
        attributes(
                'Main-Class': 'app.Main'
        )
    }

    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    with jar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    zip64 = true
}

// JUnit configuration
test {
    useJUnitPlatform()
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
    }

    // Enable test execution when tests are specifically requested
    systemProperty 'gradle.test.enabled', 'true'

    // Run when test tasks are explicitly called
    onlyIf {
        isCoverageTaskRequested()
    }
}

// Integration test task
tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
    }

    shouldRunAfter test

    onlyIf {
        isTestTaskRequested()
    }
}

// Helper method to check if test tasks are requested
def isTestTaskRequested() {
    def requestedTasks = gradle.startParameter.taskNames
    def testTasks = ['test', 'integrationTest', 'testAll', 'showCoverage', 'jacocoTestReport']
    return testTasks.any { testTask ->
        requestedTasks.any { requestedTask ->
            requestedTask.contains(testTask)
        }
    }
}

// Helper method for coverage verification (includes itself in the task list)
def isCoverageTaskRequested() {
    def requestedTasks = gradle.startParameter.taskNames
    def testTasks = ['test', 'integrationTest', 'testAll', 'showCoverage',
                     'jacocoTestReport', 'jacocoTestCoverageVerification']
    return testTasks.any { testTask ->
        requestedTasks.any { requestedTask ->
            requestedTask.contains(testTask)
        }
    }
}

// Checkstyle configuration
checkstyle {
    toolVersion = '10.12.7'
    configFile = file('config/checkstyle/checkstyle.xml')
    ignoreFailures = true
    maxWarnings = 100
}

tasks.withType(Checkstyle).configureEach {

    source = sourceSets.main.allJava
    exclude {
        it.file.absolutePath.contains('src/main/java/imports/')
    }
}

// JaCoCo configuration
jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    executionData fileTree(layout.buildDirectory.dir("jacoco")).include("*.exec")

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/Main.class'
            ])
        }))
    }

    finalizedBy jacocoTestCoverageVerification

    onlyIf {
        isTestTaskRequested()
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.01
            }
        }
    }

    onlyIf {
        isTestTaskRequested()
    }
}

// Enhanced coverage summary task
tasks.register('showCoverage') {
    dependsOn jacocoTestReport
    group = 'verification'
    description = 'Shows test coverage summary'

    doLast {
        def xmlReportFile = layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml").get().asFile
        def htmlReportFile = layout.buildDirectory.file("reports/jacoco/test/html/index.html").get().asFile

        if (xmlReportFile.exists()) {
            try {
                def parser = new XmlParser()
                parser.setFeature("https://apache.org/xml/features/disallow-doctype-decl", false)
                def xml = parser.parse(xmlReportFile)

                def counter = xml.counter.find { it.@type == 'LINE' }
                if (counter) {
                    def covered = counter.@covered.toInteger()
                    def missed = counter.@missed.toInteger()
                    def total = covered + missed
                    def percentage = total > 0 ? (covered * 100 / total).round(1) : 0

                    println "\nðŸ“Š Code Coverage Summary:"
                    println "Lines Covered: ${covered}/${total} (${percentage}%)"
                    println "Coverage Status: ${percentage >= 9 ? 'âœ… PASSED' : 'âŒ FAILED'} (minimum: 9%)"

                    if (htmlReportFile.exists()) {
                        println "ðŸ“„ Detailed HTML Report: file://${htmlReportFile.absolutePath}"
                    }
                } else {
                    println "\nðŸ“Š Code Coverage: Report generated successfully"
                }
            } catch (Exception e) {
                println "\nðŸ“Š Code Coverage: Tests completed successfully"
                println "âš ï¸  Coverage report parsing failed: ${e.message}"
            }
        } else {
            println "\nðŸ“Š Code Coverage: No coverage data found"
        }
    }
}

// Comprehensive test task
tasks.register('testAll') {
    dependsOn test, integrationTest, showCoverage
    group = 'verification'
    description = 'Runs all tests and displays coverage summary'

    doFirst {
        println "\nðŸš€ Running all tests with coverage analysis..."
    }
}

//tasks.named("checkstyleMain") {
//    dependsOn("NormalizeCDKFImports")
//}
//
//tasks.named("checkstyleTest") {
//    dependsOn("NormalizeCDKFImports")
//}
//
//tasks.register("NormalizeCDKFImports") {
//
//    onlyIf {
//        file("$projectDir/src/main/java/imports").exists()
//                && file("$projectDir/src/main/java/imports").listFiles().length > 0
//    }
//
//    doLast {
//        def generatedDir = file("src/main/java/imports")
//        def targetDir = file("$projectDir/imports")
//
//        if (!targetDir.exists()) {
//            targetDir.mkdirs()
//        }
//
//        if (generatedDir.exists() && generatedDir.listFiles().length > 0) {
//            println "ðŸ“¦ Moving generated imports from $generatedDir to $targetDir"
//            copy {
//                from(generatedDir)
//                into(targetDir)
//            }
//            delete("src/main")
//            println "Moved Java imports into $targetDir"
//        } else {
//            println "No generated imports found in $generatedDir"
//        }
//    }
//}